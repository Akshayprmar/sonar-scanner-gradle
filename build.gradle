#!groovy
// The above triggers groovy syntax highlighting in vim

plugins {
  id "com.gradle.plugin-publish" version "0.9.5"
  // Eat your own dog food :)
  id "org.sonarqube" version "2.1-rc3"
  id "com.jfrog.artifactory" version "4.4.4"
  id "com.github.hierynomus.license" version "0.13.1"
  id "net.researchgate.release" version "2.3.1"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'


group = 'org.sonarsource.scanner.gradle'
description = 'Gradle plugin to help analyzing projects with SonarQube'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
  options.encoding = 'UTF-8'
}

javadoc {
  options.encoding = 'UTF-8'
}

repositories {
  jcenter()
  mavenLocal()
  mavenCentral()
  maven {
    url "https://repox.sonarsource.com/sonarsource"
  }
}

dependencies {
  compile gradleApi()
  compile 'org.sonarsource.scanner.api:sonar-scanner-api:2.8-build684'
  compileOnly 'com.android.tools.build:gradle:2.2.0-rc2'
  testCompile localGroovy()
  testCompile 'junit:junit:4.11'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
  }
}

pluginBundle {
  website = 'http://redirect.sonarsource.com/doc/gradle.html'
  vcsUrl = 'https://github.com/SonarSource/sonar-scanner-gradle'

  description = 'Gradle plugin for running SonarQube analysis.'
  tags = ['sonarqube', 'sonar', 'quality', 'qa']

  plugins {

    sonarqubePlugin {
      id = 'org.sonarqube'
      displayName = 'SonarQube Scanner for Gradle'
    }
  }

  mavenCoordinates {
    groupId = "org.sonarsource.scanner.gradle"
    artifactId = "sonarqube-gradle-plugin"
  }
}

sonarqube {
  properties {
    property 'sonar.projectName', 'SonarQube Scanner for Gradle'
  }
}

license {
  strictCheck true
}

jacoco {
  toolVersion = "0.7.6.201602180812"
}


artifactory {
  clientConfig.setIncludeEnvVars(true)
  clientConfig.setEnvVarsExcludePatterns('*password*,*PASSWORD*,*secret*,*MAVEN_CMD_LINE_ARGS*,sun.java.command,*token*,*TOKEN*,*LOGIN*,*login*')
  contextUrl = System.getenv('ARTIFACTORY_URL')
  publish {    
    repository {
      repoKey = System.getenv('ARTIFACTORY_DEPLOY_REPO')
      username = System.getenv('ARTIFACTORY_DEPLOY_USERNAME')
      password = System.getenv('ARTIFACTORY_DEPLOY_PASSWORD')      
    }
    defaults {
      properties = [
        'vcs.revision': (System.getenv('GIT_COMMIT') ?: System.getenv('TRAVIS_COMMIT')),
        'vcs.branch': (System.getenv('GIT_BRANCH') ?: System.getenv('TRAVIS_BRANCH')),
        'build.name': 'sonar-scanner-gradle',
        'build.number': (System.getenv('BUILD_ID') ?: System.getenv('TRAVIS_BUILD_NUMBER'))
      ]
      publishConfigs('archives', 'published')
      publishPom = true // Publish generated POM files to Artifactory (true by default)
      publishIvy = false // Publish generated Ivy descriptor files to Artifactory (true by default)
    }
  }  
}

afterReleaseBuild.dependsOn publishPlugins

